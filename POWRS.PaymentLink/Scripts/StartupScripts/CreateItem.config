<?xml version="1.0" encoding="utf-8"?>
<ServiceConfiguration xmlns="http://waher.se/Schema/ServiceConfiguration.xsd">
	<StartupScript>
		<![CDATA[
Global.["CreateItem_nenad"]:= (CreateItem_nenad(ValidatedUser, OrderNum, isEcommerce, 
                                    Title, Price, Currency, IpsOnly,
									Description, PaymentDeadline, DeliveryDate, TotalNumberOfPayments, FirstPaymentAmount, LinkType,
                                    BuyerFirstName, BuyerLastName, BuyerEmail, BuyerPhoneNumber,
									BuyerAddress , BuyerCity, BuyerCountryCode, 
									CallBackUrl, WebPageUrl, SuccessUrl, ErrorUrl, LinkCreatorName, LogActor)):=
(
   try
	(				 
        logEventID := "CreateItem_nenad.config";
		logObject := ValidatedUser.username;
		
		Password:= select top 1 Password from BrokerAccounts where UserName = ValidatedUser.username;
		
		
		PayspotServiceConfiguration:= POWRS.Payment.PaySpot.ServiceConfiguration.GetCurrent() ??? null;
		if(PayspotServiceConfiguration == null) then 
		(
			Error("Payment service not configured properly.");
		);
		
		errors:= Create(System.Collections.Generic.List, System.String);
		
		if(Price < PayspotServiceConfiguration.MinimumAmount) then 
		(
			Error("Minimum amount is: " +  PayspotServiceConfiguration.MinimumAmount);
		)
		else if(Price > PayspotServiceConfiguration.MaximumAmount) then 
		(
			Error("Maximum amount is: " +  PayspotServiceConfiguration.MaximumAmount);
		);
		
        if(Global.RegexValidation(OrderNum, "OrderNumber", "") == false) then 
        (
			errors.Add("orderNum");
		);
        if(Global.RegexValidation(Title, "OrderTitle", "") == false) then
		(
			errors.Add("title");
		);
		if(Global.RegexValidation(Currency, "Currency", "") == false) then 
		(
			errors.Add("currency");
		);
		if(Global.RegexValidation(Description, "OrderDescription", "") == false) then
		(
			errors.Add("description");
		);
		if(Global.RegexValidation(PaymentDeadline, "DateDDMMYYYY", "") == false) then 
		(
			errors.Add("paymentDeadline");
		);
		if(Global.RegexValidation(BuyerFirstName, "OrderBuyerFirsLastName", "") == false) then 
		(
			errors.Add("buyerFirstName");
		);

		if(Global.RegexValidation(BuyerLastName, "OrderBuyerFirsLastName", "") == false) then
		(
			errors.Add("buyerLastName");
		);
		if(Global.RegexValidation(BuyerEmail, "Email", "") == false) then 
		(
			errors.Add("buyerEmail");
		);
		
		if(exists(BuyerPhoneNumber)) then
		(
			if(System.String.IsNullOrWhiteSpace(BuyerPhoneNumber) or Global.RegexValidation(BuyerPhoneNumber, "PhoneNumber", "") == false) then 
			(
				errors.Add("buyerPhoneNumber");
			);
		)
		else
		(
			 BuyerPhoneNumber := "";
		);
		
		if(Global.RegexValidation(BuyerAddress, "Address", "") == false) then 
		(
			errors.Add("buyerAddress");
		);
		 
		if (!exists(BuyerCity)) then BuyerCity := "";

		if (Global.RegexValidation(BuyerCity, "OrderCity", "") == false) then 
		(
			errors.Add("buyerCity");
		);

		if(Global.RegexValidation(BuyerCountryCode, "CountryCode", "") == false) then 
		(
			errors.Add("buyerCountry");
		);
		
		if(!System.String.IsNullOrEmpty(SuccessUrl)) then
		(
			if(!ValidateUrl(SuccessUrl)) then 
			(
				errors.Add("SuccessUrl");
			);
		);
		
		if(!System.String.IsNullOrEmpty(ErrorUrl)) then
		(
			if(!ValidateUrl(ErrorUrl)) then 
			(
				errors.Add("ErrorUrl");
			);
		);

        dateTemplate:= "dd/MM/yyyy HH:mm:ss";
		PaymentDeadline += " 23:59:59";
		ParsedDeadlineDate:= System.DateTime.ParseExact(PaymentDeadline, dateTemplate, System.Globalization.CultureInfo.CurrentUICulture).ToUniversalTime();
		if(ParsedDeadlineDate < NowUtc) then 
		(
			errors.Add("paymentDeadline");
		);
		
		if(errors.Count > 0) then
		(
		   Error(errors);
	    );
		
		baseParameters:= 
		{
			"RemoteId": OrderNum,
			"Title": Title,
			"Description": Description,
			"Value": Price,
			"PaymentDeadline" : ParsedDeadlineDate,
			"Currency": Currency,
			"Country": BuyerCountryCode,
			"Expires": TodayUtc.AddYears(99),			
			"BuyerFullName": BuyerFirstName + " " + BuyerLastName,
			"BuyerPhoneNumber": BuyerPhoneNumber,
			"BuyerEmail": BuyerEmail,
			"BuyerAddress": BuyerAddress,
			"BuyerCity" : BuyerCity,
			"CallBackUrl" : CallBackUrl,
			"SuccessUrl": SuccessUrl,
			"ErrorUrl": ErrorUrl,
			"LinkCreatorName": LinkCreatorName
		};
				
		if(LinkType != "OneTime" and LinkType != "Delayed" and 
		   LinkType != "Installments" and LinkType != "Subscription") then 
		   (
			errors.Add("linkType");
		   );
		
		if(LinkType == "Delayed") then 
		(
			if(System.String.IsNullOrWhiteSpace(DeliveryDate)) then
			(
				errors.Add("deliveryDate");
				Error(errors);
			)
			else if(!Global.RegexValidation(DeliveryDate, "DateDDMMYYYY", "")) then 
			(
				errors.Add("deliveryDate");
				Error(errors);
			);
			
			if((ParsedDeliveryDate:= System.DateTime.ParseExact(DeliveryDate += " 23:59:59", dateTemplate, System.Globalization.CultureInfo.CurrentUICulture).ToUniversalTime()) < ParsedDeadlineDate) then
			(			
				errors.Add("deliveryDate");
				Error(errors);
			);		
			
			if(TotalNumberOfPayments != 1) then 
			(
				errors.Add("totalNumberOfPayments");
				Error(errors);
			);
			
			baseParameters["DeliveryDate"]:= ParsedDeliveryDate;
			baseParameters["TotalNumberOfPayments"]:= TotalNumberOfPayments;
		)
		else if(LinkType == "Installments") then 
		(
			if(FirstPaymentAmount <= 0 or FirstPaymentAmount > Price or
			FirstPaymentAmount < PayspotServiceConfiguration.MinimumAmount or 
			FirstPaymentAmount > PayspotServiceConfiguration.MaximumAmount) then
			(
				errors.Add("firstPaymentAmount");
			);
			if(TotalNumberOfPayments < 2 or TotalNumberOfPayments > 24) then 
			(
				errors.Add("totalNumberOfPayments");
			);
			if(errors.Count > 0) then
			(
				Error(errors);
			);
			
			baseParameters["FirstInstallmentAmountParameter"]:= FirstPaymentAmount;
			baseParameters["DeliveryDate"]:= ParsedDeadlineDate;
			baseParameters["TotalNumberOfPayments"]:= TotalNumberOfPayments;
		)
		else if(LinkType == "Subscription") then
		(
			if(TotalNumberOfPayments < 2 or TotalNumberOfPayments > 24) then 
			(
				errors.Add("totalNumberOfPayments");
				Error(errors);
			);
			
			baseParameters["DeliveryDate"]:= ParsedDeadlineDate;
			baseParameters["TotalNumberOfPayments"]:= TotalNumberOfPayments;
		)
		else 
		(
			baseParameters["IsEcommerce"]:= isEcommerce;
			baseParameters["WebPageUrl"]:= WebPageUrl;
		);
		
		KeyId := GetSetting(ValidatedUser.username + ".KeyId","");
		KeyPassword:= GetSetting(ValidatedUser.username + ".KeySecret","");
							
		if(System.String.IsNullOrEmpty(KeyId) or System.String.IsNullOrEmpty(KeyPassword)) then 
		(
			Error("No signing keys or password available for user: " + ValidatedUser.username );
		);
		
        Identity := select top 1 * from IoTBroker.Legal.Identity.LegalIdentity where Account = ValidatedUser.username And State = 'Approved';
		AgentName := Identity.FIRST + " " + Identity.MIDDLE + " " + Identity.LAST;
		if (System.String.IsNullOrEmpty(Identity.ORGBANKNUM)) then 
		(
			Error("Legal identity for this " + ValidatedUser.username + " mising bank account number");
		);
		
		baseParameters["SellerBankAccount"]:= Identity.ORGBANKNUM;
		baseParameters["SellerName"]:= System.String.IsNullOrEmpty(Identity.ORGNAME) ? AgentName : Identity.ORGNAME;
		
		businessData:= select top 1 * from POWRS.PaymentLink.Onboarding.BusinessData where UserName = ValidatedUser.username;
		
		if(LinkType == "OneTime") then 
		(
			PayoutPage:= (IpsOnly or (businessData.IPSOnly ??? false)) ? "PayoutIPS.md" : "Payout.md";
			TemplateId:= GetSetting("POWRS.PaymentLink.TemplateId", "");
		)
		else 
		(
			baseParameters["LinkType"]:= LinkType;
			PayoutPage:= "SubscriptionPayments.md";
			TemplateId:= GetSetting("POWRS.PaymentLink.RecurrenceTemplateId", "")
		);	
		
		Contract:=CreateContract(ValidatedUser.username, TemplateId, "Public", baseParameters);
		
		Nonce := Base64Encode(RandomBytes(32));

		LocalName := "ed448";
		Namespace := "urn:ieee:iot:e2e:1.0";

		S1 := ValidatedUser.username + ":" + Waher.IoTGateway.Gateway.Domain + ":" + LocalName + ":" + Namespace + ":" + KeyId;
		KeySignature := Base64Encode(Sha2_256HMac(Utf8Encode(S1),Utf8Encode(KeyPassword)));
		
		S2 := S1 + ":" + KeySignature + ":" + Nonce + ":" + ValidatedUser.legalId + ":" + Contract.ContractId + ":" + "Creator";
        RequestSignature := Base64Encode(Sha2_256HMac(Utf8Encode(S2),Utf8Encode(Password)));
        
		POST("https://" + Waher.IoTGateway.Gateway.Domain + "/Agent/Legal/SignContract",
                 {
					"keyId": KeyId,
					"legalId": ValidatedUser.legalId,
					"contractId": Contract.ContractId,
					"role": "Creator",
					"nonce": Nonce,
					"keySignature": KeySignature,
					"requestSignature": RequestSignature
                 },
				{
					"Accept" : "application/json",
					"Authorization": "Bearer " + ValidatedUser.jwt
                });
				
		ContractSigned:= false;
		Counter:= 0;
		while ContractSigned == false and Counter < 10 do 
		(
		 State:= select top 1 State from Contracts where ContractId = Contract.ContractId;
		 ContractSigned:= State == "Signed";
		  
		 Counter += 1;
		 Sleep(1000);
		);
		
		TokenId:= select top 1 TokenId from IoTBroker.NeuroFeatures.Token where OwnershipContract = Contract.ContractId;
		
		{
			"ContractId" : Contract.ContractId,
			"TokenId" : TokenId,
			"BuyerEmail": BuyerEmail,
			"BuyerPhoneNumber": BuyerPhoneNumber,
			"Currency": Currency,
			"PayoutPage": PayoutPage
		}		
	)
	catch
	(
	    Log.Error(Exception.Message, logObject, LogActor, logEventID, null);
		if(errors.Count > 0) then 
		(
			Error(errors);
		)
		else 
		(
			Error(Exception.Message);
		);
		
	)
	finally
	(
		Destroy(logEventID);
		Destroy(logObject);
		Destroy(dateTemplate);
		Destroy(PaymentDeadline);
		Destroy(ParsedDeadlineDate);
		Destroy(KeyId);
		Destroy(KeyPassword);
		Destroy(ContractParameters);
		Destroy(EscrowFee);
		Destroy(Identity);
		Destroy(AgentName);
		Destroy(Nonce);
		Destroy(LocalName);
		Destroy(Namespace);
		Destroy(S1);
		Destroy(KeySignature);
		Destroy(ContractId);
		Destroy(Role);
		Destroy(S2);
		Destroy(RequestSignature);					
	);		    
);		
            ]]>
	</StartupScript>
</ServiceConfiguration>