<?xml version="1.0" encoding="utf-8"?>
<ServiceConfiguration xmlns="http://waher.se/Schema/ServiceConfiguration.xsd">
	<StartupScript>
		<![CDATA[
			Global.["ValidateAgentApiToken"]:= (ValidateAgentApiToken(throwIfIdentityInvalid, throwIfContactNotPopulated):= 
            (
           try
			(
				if(Response != null) then 
				(
					Response.SetHeader("Access-Control-Allow-Origin","*");
				);

				if(Request == null) then 
				(
					Error("Forbidden");
				);

				header:= null;
				jwtFactory:= null;
				reason:= null;

				Request.Header.TryGetHeaderField("Authorization", header);
				PJwt:= Trim(header.Value.Replace("Bearer ", ""));

				Waher.Runtime.Inventory.Types.TryGetModuleParameter("JWT", jwtFactory);
				if(jwtFactory == null) then 
				(
					Error("Forbidden");
				);

				Token:=Create(Waher.Security.JWT.JwtToken, PJwt);
				if(!jwtFactory.IsValid(Token, reason)) then
				(
					Error("Token " + reason);
				);

				array:= Split(Token.Claims.sub, "@");
				if(array.Length != 2) then 
				(
					Error("Invalid token");
				);

				username:= Trim(array[0]);
				userDomain:= Trim(array[1]);
				gatewayDomain:= Trim(Gateway.Domain);

				if(System.String.IsNullOrWhiteSpace(username) || System.String.IsNullOrWhiteSpace(userDomain)) then
				(
					Error("Invalid user");
				);

				legalIdentity:= select top 1 Id from LegalIdentities where Account = username and State = "Approved" order by Created desc;
				isUserApproved:= !System.String.IsNullOrWhiteSpace(legalIdentity);

				if(throwIfIdentityInvalid and !isUserApproved) then
				(			        
					Error("User is not approved");
				);

				contactFilled:= false;
				orgName := "";
				
				brokerAccRole := Select top 1 * from POWRS.PaymentLink.Models.BrokerAccountRole where UserName = username;
				if(brokerAccRole != null) then (
					orgName := brokerAccRole.OrgName;
					OrganizationContactInfo:= select top 1 * from POWRS.PaymentLink.Models.OrganizationContactInformation where OrganizationName = brokerAccRole.OrgName;
					if(OrganizationContactInfo != null && OrganizationContactInfo.IsValid()) then 
					(
						contactFilled:= true;
					);
				);
				
				if(throwIfContactNotPopulated and !contactFilled) then 
				(
					Error("Contact informations are not existent or not properly populated");
				);
   
				role := POWRS.PaymentLink.Models.AccountRole.User;
				objBrokerAccountRole := 
					select top 1 * 
					from POWRS.PaymentLink.Models.BrokerAccountRole 
					where UserName = username;

				if (objBrokerAccountRole != null) then(
					role := objBrokerAccountRole.Role;
				);

				res:= {
					authenticated: true,
					legalId: legalIdentity,
					isApproved: isUserApproved,
					orgName: orgName,
					contactInformationsPopulated: contactFilled,
					username: Str(username),
					role: role.ToString(),
					jwt: Str(PJwt)
				};
			)
			catch
			(
			   Log.Error(Exception, null);
			   Forbidden(Exception.Message);
			);
           ););
        ]]>
	</StartupScript>
</ServiceConfiguration>
